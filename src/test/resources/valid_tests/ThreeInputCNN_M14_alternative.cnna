architecture ThreeInputCNN_M14_alternative(img_height=200, img_width=300, img_channels=3, classes=3){
    /*CNN used for flower grading. Model 14 of the paper.
     *Title: Multi-Input Convolutional Neural Network for Flower Grading
     *Authors: Yu Sun, Lin Zhu, Guan Wang, and Fang Zhao.
     *Year: 2017*/

    def input Z(0:255)^{img_channels, img_height, img_width} image[3]
    def output Q(0:1)^{classes} predictions

    def conv(filter, channels){
        Convolution(kernel=filter, channels=channels) ->
        Relu()
    }

    def inputGroup(index){
        [index] ->
        conv(filter=(3,3), channels=32, ->=3) ->
        Pooling(pool_type="max", kernel=(2,2), stride=(2,2))
    }

    (image[0] | image[1] | image[2]) ->
    inputGroup(index=0|..|2) ->
    Concatenate() ->
    conv(filter=(3,3), channels=64) ->
    Pooling(pool_type="max", kernel=(2,2), stride=(2,2)) ->

    FullyConnected(units=32) ->
    Relu() ->
    FullyConnected(units=classes) ->
    Softmax() ->
    predictions
}
